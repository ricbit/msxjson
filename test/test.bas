10 CLEAR 500,&H9000:SCREEN0:WIDTH40
15 ON ERROR GOTO 7000
20 BLOAD"JSON.BIN",R
25 PRINT "TESTING CAN'T CALL USR1 BEFORE USR":EX=5:A=USR1(""):GOSUB8000
26 PRINT "TESTING CAN'T CALL USR2 BEFORE USR":EX=5:A=USR2(""):GOSUB8000
30 M=6:N$="INVALID":F$="ERROR":V=0:GOSUB2000
40 M=9:N$="VALID":F$="JSON":V=-1:GOSUB2000
50 A$="JSON6.TXT":GOSUB3000
60 DATA "",2,"#0",4,"#4",2,"#4#0",4,"#4#1",4,"#4#2",0
70 DATA "# 10",4,"#11",0,"#12#0",0,"END":GOSUB4000
80 DATA "#0","0","#2","-13","#6","123.1"
90 DATA "#8","-1.24e400","#9","-1.3e+10","END":GOSUB5000
100 A$="JSON7.TXT":GOSUB3000
110 DATA "",2,"#0",5,"#1",6,"#2",7,"#3",0,"END":GOSUB 4000
120 DATA "#0","true","#1","false","#2","null","END":GOSUB5000
130 A$="JSON5.TXT":GOSUB3000
140 DATA "",1,"#0",3,"#0$",1,"#0$#0",3,"#0 $ #1$ #2$",4
150 DATA "&widget",1,"&widge",0,"&widgett",0
160 DATA "&widget&debug",3,"&widget#1$&width",4,"END":GOSUB4000
170 DATA "&widget&debug","on","#0$#1$&name","main_window"
180 DATA "#0$#3$&data","Click Here","END":GOSUB5000
190 A$="JSON9.TXT":GOSUB3000
200 DATA "#0",3,"END":GOSUB4000
210 DATA "#0",255,"END":GOSUB6000
220 PRINT "TESTING CAN'T CALL USR(STRING)":EX=13:A=USR(""):GOSUB8000
230 PRINT "TESTING CAN'T CALL USR1(INTEGER)":EX=13:A=USR1(0):GOSUB8000
240 PRINT "TESTING CAN'T CALL USR2(INTEGER)":EX=13:A=USR2(0):GOSUB8000
250 PRINT "TESTING CAN'T CALL USR1('XX')":EX=5:A=USR1("XX"):GOSUB8000
260 PRINT "TESTING CAN'T CALL USR1('#XX')":EX=5:A=USR1("#XX"):GOSUB8000
270 PRINT "TESTING CAN'T CALL USR1('#')":EX=5:A=USR1("#"):GOSUB8000
990 END
1000 OPEN A$ FOR INPUT AS 1
1010 S=LOF(1)
1020 IF S<=0 THEN CLOSE1:RETURN
1030 IF S>200 THEN M=200 ELSE M=S
1040 P$=INPUT$(M,1)
1050 FORX=1TOM:POKEAD+X-1,ASC(MID$(P$,X,1)):NEXT
1060 S=S-M:AD=AD+M
1070 GOTO 1020
2000 FOR I=1 TO M:PRINT"TESTING ";N$;" JSON";I
2010 A$=F$+RIGHT$(STR$(I),1)+".TXT":AD=&H9000:GOSUB 1000
2020 IF USR(&H9000)<>V THEN PRINT "ERROR":END
2030 NEXT
2040 RETURN
3000 AD=&H9000:GOSUB1000:A=USR(&H9000):RETURN
4000 READ B$:IF B$="END" THEN RETURN
4010 PRINT "TESTING '";B$;"' ON ";A$
4020 READ B:IF USR1(B$)<>B THEN PRINT "ERROR":END
4030 GOTO 4000
5000 READ B$:IF B$="END" THEN RETURN
5010 PRINT "TESTING V'";B$;"' ON ";A$
5020 READ C$:IF USR2(B$)<>C$ THEN PRINT "ERROR":END
5030 GOTO 5000
6000 READ B$:IF B$="END" THEN RETURN
6010 PRINT "TESTING L'";B$;"' ON ";A$
6020 READ C:IF LEN(USR2(B$))<>C THEN PRINT "ERROR":END
6030 GOTO 6000
7000 IF ERR<>EX THEN PRINT "ERROR":END
7010 EX=0:RESUME NEXT
8000 IF EX<>0 THEN PRINT "ERROR":END
8010 RETURN
